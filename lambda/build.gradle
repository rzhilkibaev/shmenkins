plugins {
  id "com.github.mgk.gradle.s3" version "1.4.0"
}

subprojects {
  apply plugin: "java"
  apply plugin: "eclipse"
  apply plugin: "idea"

  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
  }

  dependencies {
    compile     "org.slf4j:slf4j-api:1.7.23"
    
    testCompile "org.testng:testng:6.10",
                "org.assertj:assertj-core:3.6.2",
                "org.mockito:mockito-core:2.7.9"
                
    testRuntime "ch.qos.logback:logback-classic:1.2.1"
  }

  tasks.withType(Test) {
    useTestNG()
    reports.html.destination = file("${reporting.baseDir}/${name}")
  }
}

// Projects ending with "Lambda" are executable (deployable) projects.
configure(subprojects.findAll { it.name.endsWith("Lambda") }) {
  dependencies {
  runtime group: 'com.amazonaws', name: 'aws-lambda-java-log4j', version: '1.0.0'
  runtime group: 'org.slf4j',     name: 'slf4j-log4j12',         version: '1.7.23'
  }
  
  task buildJar(type: Jar) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtime
    }

    manifest {
      attributes("Class-Path": "lib/*.jar")
    }
  }
  
  s3 {
    profile = "${awsProfile}"
  }    
  
  task publish(type: com.github.mgk.gradle.S3Upload, dependsOn: buildJar) {
    file = "${jar.archivePath}"
    bucket = "${s3Bucket}"
    key = "artifacts/${jar.archiveName}"
    overwrite = true
  }
}